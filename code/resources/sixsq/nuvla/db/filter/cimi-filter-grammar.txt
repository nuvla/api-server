(* warning: do not use wildcards for 'and'/'or' expressions because
            they create an ambiguous grammar that causes the parsing
            time to increase exponentially with the number of terms *)
Filter          ::= AndExpr | AndExpr <'or'> Filter
AndExpr         ::= Comp | Comp <'and'> AndExpr
Comp            ::= Attribute EqOp Value
                    | Value EqOp Attribute
                    | Attribute RelOp OrdinalValue
                    | OrdinalValue RelOp Attribute
                    | Attribute PrefixOp StringValue
                    | StringValue PrefixOp Attribute
                    | Attribute FullTextOp StringValue
                    | StringValue FullTextOp Attribute
                    | Attribute  GeoOp GeoShapeValue
                    | WS <'('> Filter <')'> WS

FullTextOp      ::= '=='
PrefixOp        ::= '^='
EqOp            ::= '=' | '!='
RelOp           ::= '<' | '<=' | '>=' | '>'
GeoOp           ::= 'in'


Attribute       ::= WS NamespaceTerm ('/' NamespaceTerm)* WS

<NamespaceTerm> ::= (Term ':' Term) | Term
<Term>          ::= #'([a-zA-Z][\w-]*[\w]+)|[a-zA-Z]'
<OrdinalValue>  ::= IntValue | DateValue | StringValue
<NominalValue>  ::= BoolValue | NullValue
<Value>         ::= OrdinalValue | NominalValue
IntValue        ::= WS #'\d+' WS
DateValue       ::= WS #'\d+-\d+(-\d+)?(T\d+:\d+:\d+(\.\d+)?(Z|[+-]\d+:\d+))?' WS
<StringValue>   ::= WS (DoubleQuoteString | SingleQuoteString) WS
BoolValue       ::= WS ('true' | 'false') WS
NullValue       ::= WS 'null' WS
LongitudeValue  ::= WS #'[-+]?(180(\.0+)?|((1[0-7]\d)|([1-9]?\d))(\.\d+)?)' WS
LatitudeValue   ::= WS #'[-+]?([1-8]?\d(\.\d+)?|90(\.0+)?)' WS
PointValue      ::= WS <'['> LongitudeValue <','?> LatitudeValue <']'> WS
EnvelopeValue   ::= WS <'ENVELOPE['>  WS PointValue <','?> PointValue <']'> WS
PolygonValue    ::= WS <'POLYGON['> WS PointValue (<','?> PointValue)* <']'> WS
<GeoShapeValue> ::= WS (EnvelopeValue | PolygonValue) WS

<WS>              ::= <#'\s*'>

DoubleQuoteString ::= #"\"[^\"\\]*(?:\\.[^\"\\]*)*\""
SingleQuoteString ::= #"'[^'\\]*(?:\\.[^'\\]*)*'"
